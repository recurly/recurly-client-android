apply plugin: 'com.android.library'

apply plugin: 'maven'
apply plugin: 'signing'

version = "0.1.3"
group = "com.recurly"
archivesBaseName = "android-sdk"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.0"
        testApplicationId "com.recurly.android.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.code.gson:gson:2.2.4'
    // NOTE: when updating volley version, update UA_NETWORK_VERSION in RecurlyNetwork.java as well
    compile 'com.mcxiaoke.volley:library:1.0.15@aar'
    compile 'com.android.support:support-v4:20.0.0'
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Recurly Android SDK'
            packaging 'aar'
            description 'Android SDK for Recurly payment services'
            url 'https://www.recurly.com'

            scm {
                url 'scm:git@github.com/recurly-client-android.git'
                connection 'scm:git@github.com/recurly-client-android.git'
                developerConnection 'scm:git@github.com/recurly-client-android.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'peter.hsu'
                    name 'Peter Hsu'
                    email 'peter.hsu@recurly.com'
                }
            }
        }
    }
 
}

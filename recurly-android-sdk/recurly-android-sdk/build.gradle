apply plugin: 'com.android.library'

apply plugin: 'maven'
apply plugin: 'signing'


// NOTE: update RecurlyApi#VERSION as well
version = "1.0.1"
group = "com.recurly"
archivesBaseName = "android-sdk"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.0"
        testApplicationId "com.recurly.android.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.code.gson:gson:2.2.4'
    // NOTE: update RecurlyNetwork#UA_NETWORK_VERSION as well
    implementation 'com.mcxiaoke.volley:library:1.0.15@aar'
    implementation 'com.android.support:support-v4:23.4.0'
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        doFirst { classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar) }
        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.overview = "src/main/java/com/recurly/android/overview.html"
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

if (project.hasProperty("sonatypeUsername")){
uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Recurly Android SDK'
            packaging 'aar'
            description 'Android SDK for Recurly payment services'
            url 'https://www.recurly.com'

            scm {
                url 'scm:git@github.com:ayyoitsp/recurly-client-android.git'
                connection 'scm:git@github.com:ayyoitsp/recurly-client-android.git'
                developerConnection 'scm:git@github.com:ayyoitsp/recurly-client-android.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'support'
                    name 'Recurly Support'
                    email 'support@recurly.com'
                }
            }
        }
    }

}
}
